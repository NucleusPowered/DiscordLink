plugins {
    id 'java'
    id 'idea'
    id 'ninja.miserable.blossom' version '1.0.1'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'org.spongepowered.plugin' version '0.6'
    id 'maven'
}

project.ext.suffix = "-SNAPSHOT"

group = 'io.github.nucleuspowered.phonon'
version = '0.1.0-SNAPSHOT'
description = 'An experimental link between Discord & Minecraft'
ext.url = 'http://nucleuspowered.org'

repositories {
    jcenter()
    maven {
        name = 'sponge'
        url = 'http://repo.spongepowered.org/maven'
    }
    maven {
        name 'drnaylor-minecraft'
        url 'http://repo.drnaylor.co.uk/artifactory/list/minecraft'
    }
    maven {
        name 'drnaylor'
        url 'http://repo.drnaylor.co.uk/artifactory/list/quickstart'
    }
}

def qsmlDep = 'uk.co.drnaylor:quickstart-moduleloader:0.6.0'
def jdaDep = 'net.dv8tion:JDA:3.0.BETA2_143'
def spongeapi = '5.1.0'

dependencies {
    compile('org.spongepowered:spongeapi:' + spongeapi)
    compile(qsmlDep)
    compile(jdaDep)
}

jar {
    manifest {
        attributes  'Implementation-Title': project.name,
                'Implementation-Version': version,
                'SpongeAPI-Version': spongeapi,
                'Git-Hash': getGitHash()
    }

    archiveName = "Phonon-${version}-base.jar"
}

shadowJar {
    dependencies {
        include dependency(jdaDep)
        include dependency(qsmlDep)
        include dependency('org.apache.httpcomponents:httpcore:4.4.4')
        include dependency('org.apache.httpcomponents:httpclient:4.5.2')
        include dependency('org.apache.httpcomponents:httpcore-nio:4.4.4')
        include dependency('org.apache.httpcomponents:httpasyncclient:4.1.1')
        include dependency('com.mashape.unirest:unirest-java:1.4.9')
        include dependency('org.apache.commons:commons-lang3:3.5')
        include dependency('commons-logging:commons-logging:1.2')
        include dependency('org.json:json:20160810')
        include dependency('com.neovisionaries:nv-websocket-client:1.30')
        include dependency('org.apache.commons:commons-collections4:4.1')
        include dependency('net.java.dev.jna:jna:4.2.2')
        include dependency('junit:junit:4.12')

    }

    relocate 'org.apache', 'io.github.nucleuspowered.phonon.relocate.org.apache'
    relocate 'org.json', 'io.github.nucleuspowered.phonon.relocate.org.json'
    relocate 'com.neovisionaries.ws.client', 'io.github.nucleuspowered.phonon.relocate.com.neovisionaries.ws.client'
    relocate 'net.dv8tion.jda', 'io.github.nucleuspowered.phonon.relocate.net.dv8tion.jda'
    relocate 'uk.co.drnaylor', 'io.github.nucleuspowered.phonon.relocate.uk.co.drnaylor'
    archiveName = "Phonon-${version}-plugin.jar"
}

blossom {
    def location = 'src/main/java/io/github/nucleuspowered/phonon/PluginInfo.java'

    replaceToken '@name@', project.name, location
    replaceToken '@version@', version, location
    replaceToken '@description@', project.description, location
    replaceToken '@url@', project.url, location
}

build.dependsOn(shadowJar)

static String getGitHash() {
    def process = 'git rev-parse --short HEAD'.execute()
    process.waitFor()
    return process.exitValue() ? 'unknown' : process.text.trim()
}